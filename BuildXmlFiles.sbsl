#required Common/Path.sbsl
#required Common/Zip.sbsl
#required Common/Log.sbsl

method Script(CliPath: String, ProjectPath: String, DestinationFolder: String = "", ArchiveName: String = "")
    if not Path.Exists(CliPath)
        Log.e("Couldn't find edt cli executable: %CliPath")
    ;

    if not Path.Exists(ProjectPath)
        Log.e("Couldn't find project folder: %ProjectPath")
    ;

    if DestinationFolder == ""
        DestinationFolder = new File(ProjectPath).Path
    ;

    if not Path.Exists(DestinationFolder)
        Log.e("Couldn't find build destination folder: %DestinationFolder")
    ;

    if ArchiveName == ""
        ArchiveName = new File(ProjectPath).Name
    ;
    
    val DataPath = Files.CreateTempDirectory()
    val BuildPath = Files.CreateTempDirectory()

    val Args = [
        "-data",
        DataPath,
        "-command",
        "export",
        "--project",
        ProjectPath,
        "--configuration-files",
        BuildPath
    ]

    val EdtCli = new OsProcess(CliPath, Args, True)
    EdtCli.Start()
    EdtCli.WaitForCompletion()

    val ZipPath = Path.Join(DestinationFolder, "%ArchiveName.zip")
    Zip.CreateFromDirectory(BuildPath.Path, ZipPath)
    
    Log.i("Zipped project path: ${ZipPath}")
;