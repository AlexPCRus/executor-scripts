#required Common/Path.sbsl
#required Common/Zip.sbsl

method BuildXmlFiles(CliPath: String, ProjectPath: String, DestinationFolder: String = "", ArchiveName: String = ""): Number
    if not Path.Exists(CliPath)
        throw new IllegalArgumentException("Couldn't find edt cli executable by passed path")
    ;

    if not Path.Exists(ProjectPath)
        throw new IllegalArgumentException("Couldn't find project folder by passed path")
    ;

    if DestinationFolder == ""
        DestinationFolder = new File(ProjectPath).Path
    ;

    if not Path.Exists(DestinationFolder)
        throw new IllegalArgumentException("Couldn't find destination folder by passed path")
    ;

    if ArchiveName == ""
        ArchiveName = new File(ProjectPath).Name
    ;
    
    val DataPath = Files.CreateTempDirectory()
    val BuildPath = Files.CreateTempDirectory()

    val Args = [
        "-data",
        DataPath,
        "-command",
        "export",
        "--project",
        ProjectPath,
        "--configuration-files",
        BuildPath
    ]

    val EdtCli = new OsProcess(CliPath, Args, True)
    EdtCli.Start()
    EdtCli.WaitForCompletion()

    Files.Delete(DataPath)

    val ArchivePath = Path.Join(DestinationFolder, "%ArchiveName.zip")
    Zip.ZipFolder(BuildPath.Path, ArchivePath)
    Console.Write("Zipped project path: ${ArchivePath}")

    Files.Delete(BuildPath)

    return EdtCli.GetExitCode()
;